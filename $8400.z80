;Tab:4 Shift_JIS
;
;$8400.$$$

	relaxed on

	ORG			$8400

Disk_Load2		equ	$B000+6
Disk_FileName	equ	$B000+30

StartUp:
	ld			sp,$8400
	ld			a,%00111001				;ROMバンクに戻す
	out			($31),a
	ld			a,%10001001				;bit4=0 TVRAM
	out			($32),a

	xor			a
	ld			($E6A7),a				;カーソル表示フラグ 0=非表示 1=表示
	call		$428B					;カーソル点滅停止
	call		$4021					;ファンクションキー表示停止
	ld			bc,80*256+25
	call		$6F6B
	call		$5F0E					;テキスト画面クリア

	ld			a,%00111011				;64KB RAM mode TextWindow機能OFF
	out			($31),a
	ld			a,%10011001				;bit4=1 Main RAM
	out			($32),a

	di
	xor			a						;割り込み設定
	out			($E6),a
	out			($E4),a					;割り込みレベル設定

	out			($5F),a					;$C000-$FFFFをメインRAMに割り当て



	call		CheckSound
	ld			a,(OPNA_REG)
	or			a
	jp			z,Key

	in			a,(8)					;SHIFT押しながら起動してたら再生しない
	bit			6,a
	jp			z,Key

;ADPCMとPCMを同時に再生できるか   4MHzで調整
Play:
	ld			hl,$F3C8
	call		PrintMSG
	db			"adpcm data setting now.",0

	ld			hl,0
	ld			(ADPCM_ADR),hl
	ld			(ADPCM_START),hl
	call		Disk_Load2
	db			"KINOKO  IMA",0

	call		Disk_FileName
	ld			hl,11
	add			hl,de
	ld			a,(hl)					;=FileSize
	inc			hl
	ld			h,(hl)
	ld			l,a

	ld			de,32
	or			a
	ld			bc,0
.loop:
	inc			bc
	sbc			hl,de
	jr			nc,.loop
	ld			(ADPCM_END),bc

	call		ADPCM_Write

	ld			hl,0



	ld			a,(OPNA_REG)
	ld			c,a
	ld			hl,0
	call		ADPCM_Stop

	ld			hl,$F3C8
	call		PrintMSG
	db			"                              ",0


	call		ADPCM_Play

Key:
	ld			hl,0xF3C8
	call		PrintMSG
	db			"hit space to show next CG(0).",0
N:

	in			a,(9)
	bit			6,a
	jr			nz,Key

	xor a
	out (81),a		;ポート81に0で　テキスト画面を消す。
	out (05ch),a
	ld			hl,0x0000
	call		Disk_Load2
FNAMEB:
	db			"1       BLZ",0
	ld			hl,0x0000+4
	ld			de,0xC000
	call			DECODE_LZE




	out (05dh),a
	ld			hl,0x0000
	call		Disk_Load2
FNAMER:
	db			"1       RLZ",0
	ld			hl,0x0000+4
	ld			de,0xC000
	call			DECODE_LZE

	out (05eh),a
	ld			hl,0x0000
	call		Disk_Load2
FNAMEG:
	db			"1       GLZ",0
	ld			hl,0x0000+4
	ld			de,0xC000
	call			DECODE_LZE

	out			($5F),a					;$C000-$FFFFをメインRAMに割り当て
	ld	a,(FNAMEB)
	inc	a
	ld	(FNAMEB),a
	ld	a,(FNAMEG)
	inc	a
	ld	(FNAMEG),a
	ld	a,(FNAMER)
	inc	a
	ld	(FNAMER),a

	sub	0x3a
	jr	z,LoopName
	jp			Key

LoopName:
	ld	a,0x31
	ld	(FNAMEB),a
	ld	(FNAMEG),a
	ld	(FNAMER),a
	jp			Key

Print:
	push		af
	and			$F0
	rrca
	rrca
	rrca
	rrca
	add			a,$90
	daa
	adc			a,$40
	daa
	ld			(hl),a
	inc			hl
	pop			af
	and			$0F
	add			a,$90
	daa
	adc			a,$40
	daa
	ld			(hl),a
	inc			hl
	ret

PrintMSG:
	ld			a,%10001001
	out			($32),a
	ex			de,hl
	ex			(sp),hl
.loop:
	ldi
	ld			a,(hl)
	or			a
	jr			nz,.loop
	ex			(sp),hl
	ex			de,hl
	ld			a,%10011001
	out			($32),a
	ret

	include		"adpcm.z80"
	include		"device88.z80"
	include		"lzedecR.mac"
