;Tab:4 Shift_JIS
;
;$8400.$$$

	relaxed on

	ORG			$8400

Disk_Load2		equ	$F800+6
Disk_FileName	equ	$F800+30

StartUp:
	ld			sp,$8400
	ld			a,%00111001				;ROMバンクに戻す
	out			($31),a
	ld			a,%10001001				;bit4=0 TVRAM
	out			($32),a

	xor			a
	ld			($E6A7),a				;カーソル表示フラグ 0=非表示 1=表示
	call		$428B					;カーソル点滅停止
	call		$4021					;ファンクションキー表示停止
	ld			bc,80*256+25
	call		$6F6B
	call		$5F0E					;テキスト画面クリア

	ld			a,%00111011				;64KB RAM mode TextWindow機能OFF
	out			($31),a
	ld			a,%10011001				;bit4=1 Main RAM
	out			($32),a

	di
	xor			a						;割り込み設定
	out			($E6),a
	out			($E4),a					;割り込みレベル設定

	out			($5F),a					;$C000-$FFFFをメインRAMに割り当て

	call		CheckSound
	ld			a,(OPNA_REG)
	or			a
	jp			z,$


;ADPCMとPCMを同時に再生できるか   4MHzで調整
Test:
	ld			hl,$F3C8
	call		PrintMSG
	db			"adpcm data setting now.",0

	ld			hl,0
	ld			(ADPCM_ADR),hl
	ld			(ADPCM_START),hl
	call		Disk_Load2
	db			"SYNTH   BIN",0

	call		Disk_FileName
	ld			hl,11
	add			hl,de
	ld			a,(hl)					;=FileSize
	inc			hl
	ld			h,(hl)
	ld			l,a

	ld			de,32
	or			a
	ld			bc,0
.loop:
	inc			bc
	sbc			hl,de
	jr			nc,.loop
	ld			(ADPCM_END),bc

	call		ADPCM_Write

	ld			hl,0
	call		Disk_Load2
	db			"CLAP    SND",0

	call		Disk_FileName
	ld			hl,11
	add			hl,de
	ld			a,(hl)					;=FileSize
	inc			hl
	ld			h,(hl)
	ld			l,a
	ld			(ClapFileSize),hl

	ld			hl,$F3C8
	call		PrintMSG
	db			"hit space to playback synth sound (adpcm).",0
	ld			hl,$F3C8+120
	call		PrintMSG
	db			"hit return to playback clap sound (pcm).",0

Key:
	ld			a,(OPNA_REG)
	ld			c,a
	ld			hl,0

	in			a,(1)
	bit			7,a
	jr			z,Clap
	in			a,(9)
	bit			6,a
	jr			nz,Key

	call		ADPCM_Stop
	call		ADPCM_Play
	jr			Key

ClapFileSize:	dw		0


Clap:
	ld			de,$01CE				;LR出力 sample DA x8 DRAM
	call		WriteFM2
.wait:
	ld			a,26					;7
.wait2:
	dec			a
	jr			nz,.wait2				;16*25+12

	ld			a,$0E					;7
	out			(c),a					;12
	inc			c						;4
	outi								;16
	dec			c						;4
	ld			a,(ClapFileSize+1)		;13
	cp			h						;4
	jr			nz,.wait				;12=491
	ld			a,(ClapFileSize)
	cp			l
	jr			nz,.wait
	jr			Key


Print:
	push		af
	and			$F0
	rrca
	rrca
	rrca
	rrca
	add			a,$90
	daa
	adc			a,$40
	daa
	ld			(hl),a
	inc			hl
	pop			af
	and			$0F
	add			a,$90
	daa
	adc			a,$40
	daa
	ld			(hl),a
	inc			hl
	ret

PrintMSG:
	ld			a,%10001001
	out			($32),a
	ex			de,hl
	ex			(sp),hl
.loop:
	ldi
	ld			a,(hl)
	or			a
	jr			nz,.loop
	ex			(sp),hl
	ex			de,hl
	ld			a,%10011001
	out			($32),a
	ret

	include		"adpcm.z80"
	include		"device88.z80"

